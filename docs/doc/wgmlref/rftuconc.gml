:H2.Notes and Paragraph Continuation
:pc.There are other GML entities which process words and input lines
in a way that is similar to paragraphs.
This section introduces two other entities&gml. paragraph
continuation (:hp2.&gml.pc:ehp2.)
:I2 refid='ttpc'.ttpc
and note (:hp2.&gml.note:ehp2.).
:I2 refid='ttnote'.ttnote
:p.Paragraph continuation is used when you wish to continue
a paragraph which has been interrupted by another GML entity.
If the text following the example introduced in the preceding
section is a continuation of the
paragraph before the example, then the GML source should be altered
as follows&gml.
:fig id=rfgpc place=inline frame=box.
 :INCLUDE file='rfgpc'.
 :figcap.Illustration of Paragraph Continuation
:efig.
:pc.Note that the :hp2.&gml.pc:ehp2. tag has been used instead of the
:hp2.&gml.p:ehp2. tag in the text following the example document element.
:INCLUDE file='rffhead'.
:fig place=inline frame=box.
 :INCLUDE file='rflpc'.
 :figcap.
 :figdesc.Output of :figref refid=rfgpc page=no.
:efig.
:PC.With the layout used for this tutorial,
the first line of the text following
the example is not indented, causing it to appear as if it
were a continuation of the paragraph which precedes the
example.
:p.It is tempting to use the :hp2.&gml.p:ehp2.
:I2 refid='ttp'.ttp
and :hp2.&gml.pc:ehp2.
:I2 refid='ttpc'.ttpc
tags interchangeably when it is known that they are processed
in an indistinguishable fashion with a given layout.
This practice is to be discouraged since it may prevent a
document from being formatted properly should the layout
be changed.
:p.
The paragraph and paragraph continuation tags process input lines
together.
When the end of an input line is reached,
the input text on the next input line is considered to
be the start of a new word.
:p.
Another GML entity which processes input lines together is a
:HP2.note:eHP2..
Consider the following GML specification&gml.
:fig id=rfgnote place=inline frame=box.
 :INCLUDE file='rfgnote'.
 :FIGCAP.Illustration of the Note Entity
:efig.
:INCLUDE file='rffhead'.
:fig place=inline frame=box.
 :INCLUDE file='rflnote'.
 :figcap.
 :figdesc.Output of :figref refid=rfgnote page=no.
:efig.
:pc.The :HP2.&gml.note:eHP2. tag is used for a block of
:I2 refid='ttnote'.ttnote
text that is to be specially noted.
:p.The following tags have been introduced in this section&gml.
:dl.
:dt.&gml.pc
:I2 refid='ttpc'.ttpc
:dd.The tag for paragraph continuation is used to indicate
the continuation of a paragraph interrupted by another
GML entity.
:dt.&gml.note
:I2 refid='ttnote'.ttnote
:dd.This tag is used for a paragraph which is to be
specially noted.
The text "Note:\b" is generated by &WGML..
:edl.
:p.A number of tags are said to be followed by
:hp2.paragraph elements:ehp2..
:I1.paragraph elements
Paragraph elements are certain GML tags and words of text.
The words of text are processed
together according to the style dictated by the layout
with which the document is being formatted.
The :hp2.&gml.p:ehp2., :hp2.&gml.pc:ehp2. and :hp2.&gml.note:ehp2. tags
are all assumed to be followed by paragraph elements.
